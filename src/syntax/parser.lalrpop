use crate::typing::types::*;
use crate::syntax::ast::*;
use super::tokens::*;
use std::collections::HashMap;

grammar<'input>(input: &'input str);

pub IV: Module = Module => <>;

Module: Module = {
    <ds:Defs> => {
        let (data_defs, op_defs) = ds;
        Module::new(data_defs, op_defs)
    },
};

Defs: (HashMap<String, DataDef>, HashMap<String, OpDef>) = {
    => (HashMap::new(), HashMap::new()),
    <mut ds:Defs> <nd:DataDef> => {
        let (n, d) = nd;
        ds.0.insert(n, d);
        ds
    },
    <mut ds:Defs> <no:OpDef> => {
        let (n, o) = no;
        ds.1.insert(n, o);
        ds
    },
};

DataDef: (String, DataDef) = {
    <start:@L> "data" <name:"uident"> <params:"lident"*> ":" <constrs:Comma<DataConstr>> "." <end:@R> => {
        let params = params.into_iter().map(|s| s.to_owned()).collect();
        let constrs = constrs.into_iter().collect();
	let span = Span { start, end };
        (name.to_owned(), DataDef { params, constrs, span })
    },
};

DataConstr: (String, DataConstr) = {
    <start:@L> <name:"lident"> <end:@R> => (name.to_owned(), DataConstr { params: vec![], span: Span { start, end } }),
    <start:@L> "[" <params:Comma<Type>> "]" <name:"lident"> <end:@R> => (name.to_owned(), DataConstr { params, span: Span { start, end } }),
};

Type: Type = {
    #[precedence(level="0")]
    <name:"lident"> => Type::Poly(name.to_owned()),
    <name:"uident"> => Type::Mono(name.to_owned()),
    "[" <pre:Comma<Type>> "]" "[" <post:Comma<Type>> "]" => Type::Op(OpType { pre, post }), 
    #[precedence(level="1")] #[assoc(side="left")]
    <t1:Type> <t2:Type> => Type::App(Box::new(t1), Box::new(t2)),
};

OpDef: (String, OpDef) = {
    <start:@L> "define" "[" <pre:Comma<Type>> "]" <name:"lident"> "[" <post:Comma<Type>> "]" ":" <body:Op*> "." <end:@R> => {
        let span = Span { start, end };
        let ann = OpType { pre, post };
        let body = Body::Body(body);
        (name.to_owned(), OpDef { ann, body, span })
    },
};

Op: Op = {
    <start:@L> <lit:Literal> <end:@R> => Op::Literal { value: lit, span: Span { start, end } },
    <start:@L> <name:"lident"> <end:@R> => Op::Name { value: name.to_owned(), span: Span { start, end } },
    <start:@L> "(" <ops:Op*> ")" <end:@R> => Op::Quote { value: ops, span: Span { start, end } },
    <start:@L> "case" "{" <head_arm:CaseArm> "}" <end:@R> => {
        let span = Span { start, end };
        Op::Case { head_arm, arms: vec![], span }
    },
    <start:@L> "case" "{" <head_arm:CaseArm> "," <arms:Comma<CaseArm>> "}" <end:@R> => {
        let span = Span { start, end };
        Op::Case { head_arm, arms, span }
    },
};

Literal: Literal = {
    <n:"num"> => Literal::Int(n),
};

CaseArm: CaseArm = {
    <constr:"lident"> "{" <body:Op*> "}" => CaseArm { constr: constr.to_owned(), body },
};

Comma<T>: Vec<T> = {
    <mut v:(<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            v.push(e);
            v
        },
    },
};

extern {
    type Location = usize;
    type Error = LexingError;

    enum Token<'input> {
        "." => Token::End,
        "num" => Token::Number(<i32>),
        "lident" => Token::LIdent(<&'input str>),
        "uident" => Token::UIdent(<&'input str>),
        "define" => Token::Define,
        "data" => Token::Data,
        "case" => Token::Case,
        ":" => Token::Colon,
        "," => Token::Comma,
        "[" => Token::BracketOpen,
        "]" => Token::BracketClose,
        "(" => Token::ParenOpen,
        ")" => Token::ParenClose,
        "{" => Token::BraceOpen,
        "}" => Token::BraceClose,
    }
}